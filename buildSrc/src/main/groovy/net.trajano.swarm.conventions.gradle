plugins {
    id 'idea'
    id 'java'
    id "com.diffplug.spotless"
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
repositories {
    mavenCentral()
}
ext {
    set('springCloudVersion', "2022.0.1")
    set('springBootVersion', "3.0.3")
    set('testcontainersVersion', "1.17.6")
    set('grpcVersion', "1.53.0")
    set('protobufVersion', "3.21.12")
    // set('snakeyaml.version', '1.31')
}

jar {
    // disable plain jar creation
    enabled = false
}
spotless {
    format "misc", {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
    }
}

dependencies {
    constraints {
        implementation('org.yaml:snakeyaml') {
            version {
                reject '[0, 1.31)'
            }
            because("CVE https://security.snyk.io/vuln/SNYK-JAVA-ORGYAML-2806360")
        }

        implementation('commons-io:commons-io') {
            version {
                reject '[0, 2.7)'
            }
            because("CVE https://security.snyk.io/vuln/SNYK-JAVA-COMMONSIO-1277109")
        }
    }
}
build.dependsOn spotlessApply