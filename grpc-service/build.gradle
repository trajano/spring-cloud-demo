plugins {
    id 'org.springframework.boot' version '3.0.3'
    id 'io.spring.dependency-management' version '1.1.0'

    id 'net.trajano.swarm.conventions'
    id "com.diffplug.spotless"
    id 'jacoco'
    id "com.google.protobuf" version "0.9.2"
}

group = 'net.trajano.swarm'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    // implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-services"
    implementation "io.grpc:grpc-stub"
    //	implementation 'net.devh:grpc-server-spring-boot-starter:2.13.1.RELEASE'
    implementation "io.zipkin.brave:brave-instrumentation-grpc"
    runtimeOnly 'io.grpc:grpc-netty'
    implementation 'com.google.protobuf:protobuf-java-util'
    compileOnly 'org.projectlombok:lombok'
    implementation project(':logging')
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "io.grpc:grpc-bom:${grpcVersion}"
        mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder:tiny'
    // environment = ['BP_NATIVE_IMAGE': 'true']
}
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
tasks.named("spotlessJava").configure { dependsOn("generateProto") }
